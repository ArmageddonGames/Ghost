// import "std.zh"
// import "ghost.zh"

// A simple boss that moves up and down the side of the screen,
// periodically firing waving fireballs in three directions.
// Appears on screen 2 in the demo.

const int DRG_ENEMY_ID=183;

ffc script Dragon
{
    void run()
    {
        npc ghost;        
        int timer;
        eweapon wpn;
        
        // Initialize
        ghost=GhostInitCreate(this, DRG_ENEMY_ID);
        DrawSpawnAnimation(this, ghost);
        SetOffsets(this, ghost, 16, 0, 0, 0);
        
        this->Vx=0.2;
        this->Vy=0.3;
        
        timer=Rand(60, 180);
        
        while(true)
        {
            // Fire every 1-3 seconds
            timer--;
            if(timer<=0)
            {
                timer=Rand(60, 180);
                
                // Fire three fireballs aimed at Link, each moving in a sine wave
                for(int j=0; j<3; j++)
                {
                    wpn=FireAimedEWeapon(EW_FIREBALL, this->X+32, this->Y+32, Sign(j-1)*PI/8, 200, 2, 17, SFX_FIREBALL, 0);
                    SetEWeaponMovement(wpn, EWM_SINE_WAVE, 8);
                }
            }
            
            DrgWaitframe(this, ghost);
        }
    }
    
    
    void DrgWaitframe(ffc this,  npc ghost)
    {
        // Move with Vx and Vy; reverse if at edge of movement area
        if(this->X<=32)
            this->Vx=0.2;
        else if(this->X>=63)
            this->Vx=-0.2;
        
        if(this->Y<=32)
            this->Vy=0.3;
        else if(this->Y>=95)
            this->Vy=-0.3;
        
        GhostWaitframeF(this, ghost, true, true);
    }
}
