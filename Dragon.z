// import "std.zh"
// import "ghost.zh"

// A very simple enemy that moves up and down the side of the screen,
// periodically firing waving fireballs in three directions.

ffc script Dragon
{
    void run()
    {
        npc ghost=GhostInitCreate(this, 183);
        int timer=Rand2(60, 180);
        eweapon wpn;
        
        this->Vx=0.2;
        this->Vy=0.3;
        
        for(int i=0; true; i++)
        {
            if(i==timer)
            {
                // Fire every 1-3 seconds
                timer=Rand2(60, 180);
                i=0;
                for(int j=0; j<3; j++)
                {
                    wpn=FireAimedEWeapon(EW_FIREBALL, this->X+32, this->Y+32, Sign(j-1)*PI/8, 200, 2, true, 17, false, SFX_FIREBALL);
                    SetEWeaponMovement(wpn, EWM_SINE_WAVE, 8);
                }
            }
            
            DrgWaitframe(this, ghost);
        }
    }
    
    void DrgWaitframe(ffc this,  npc ghost)
    {
        // Move with Vx and Vy; reverse if at edge of movement area
        if(this->X<=32)
            this->Vx=0.2;
        else if(this->X>=63)
            this->Vx=-0.2;
        
        if(this->Y<=32)
            this->Vy=0.3;
        else if(this->Y>=95)
            this->Vy=-0.3;
        
        GhostWaitframeF(this, ghost, true, true);
    }
}
