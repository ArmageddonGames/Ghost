// import "std.zh"
// import "ghost.zh"

// Moves left and right for several seconds, regularly shooting splitting
// fireballs. After that, rolls into a ball and bounces around the screen.
// Appears on screen 5 in the demo quest.

const int IFL_HAND1_FFC=2;
const int IFL_HAND2_FFC=3;

const int IFL_WALK_COMBO=28;
const int IFL_ROLL_COMBO=31;
const int IFL_HAND1_COMBO=29;
const int IFL_HAND2_COMBO=30;

const int IFL_FIRE_SPRITE=21;
const int IFL_FIREBALL_SPRITE=17;

const int IFL_BATTLE_MIDI=11;
const int IFL_DUNGEON_MIDI=5;

ffc script Iflyte
{
    void run()
    {
        ffc hand1=Screen->LoadFFC(IFL_HAND1_FFC);
        ffc hand2=Screen->LoadFFC(IFL_HAND2_FFC);
        npc ghost;
        
        // Make the hands invisible initially
        hand1->Data=0;
        hand2->Data=0;
        
        ghost=GhostInitWait(this, 1, false);
        DrawSpawnAnimation(this, ghost);
        hand1->Data=IFL_HAND1_COMBO;
        hand2->Data=IFL_HAND2_COMBO;
        
        // Add extra HP so he'll stay alive until the end of his death animation;
        // otherwise, the item would appear too soon
        SetHP(this, ghost, ghost->HP+1000);
        
        Game->PlayMIDI(IFL_BATTLE_MIDI);
        
        while(true)
        {
            WalkAndShoot(this, ghost);
            RollAttack(this, ghost);
        }
    }
    
    
    // Walk back and forth at the top of the screen, regularly shooting fireballs.
    void WalkAndShoot(ffc this, npc ghost)
    {
        eweapon wpn;
        int time;
        
        // Start moving
        this->Vy=0;
        if(this->X>=104) // X=104 is centered
            this->Vx=-1.5;
        else
            this->Vx=1.5;
            
        time=Rand(180, 480); // 3-8 seconds
        
        for(int i=0; i<time; i++)
        {
            // Fire every 1.5 seconds
            if(i%90==0 && i>0)
            {
                for(int j=0; j<3; j++)
                {
                    wpn=FireEWeapon(EW_SCRIPT1, this->X+16, this->Y+16, j*PI/2, 200, 2, false, IFL_FIRE_SPRITE, true, SFX_FIREBALL);
                    SetEWeaponLifespan(wpn, EWL_SLOW_TO_HALT, 4);
                    SetEWeaponDeathEffect(wpn, EWD_4_FIREBALLS_RANDOM, IFL_FIREBALL_SPRITE);
                }
            }
            
            // If at wall, change direction
            if(this->X<=48)
                this->Vx=1.5;
            else if(this->X>=160)
                this->Vx=-1.5;
            
            IflWaitframe(this, ghost);
        }
    }
    
    
    // Roll into an invincible ball and bounce around the room.
    void RollAttack(ffc this, npc ghost)
    {
        ffc hand1=Screen->LoadFFC(IFL_HAND1_FFC);
        ffc hand2=Screen->LoadFFC(IFL_HAND2_FFC);
        float angle;
        int bounces;
        int numBounces;
        
        // Become invincible while rolling
        SetAllDefenses(ghost, NPCDT_BLOCK);
        
        // Change into a ball
        Transform(this, ghost, IFL_ROLL_COMBO, -1, 2, -1);
        hand1->Data=0;
        hand2->Data=0;
        ghost->Damage+=2;
        
        // Start moving at a random angle, constrained to a certain range;
        // if it's too horizontal or too vertical, it's easy to avoid
        angle=Rand(30, 60)+90*Rand(2);// That's 30-60 or 120-150
        // If Link's at the top of the screen, roll upward first
        if(Link->Y<=this->Y)
            angle+=180;
        this->Vx=VectorX(5, angle);
        this->Vy=VectorY(5, angle);
        
        // Bounce off the walls a certain number of times
        bounces=0;
        numBounces=Rand(15, 25);
        while(true)
        {
            // If near the edge of the screen, reverse direction
            if(this->X<=32 || this->X>=192)
            {
                bounces++;
                this->Vx=-this->Vx;
            }
            if(this->Y<=32 || this->Y>=112)
            {
                bounces++;
                this->Vy=-this->Vy;
            }
            
            // After bouncing long enough, break out of the loop when at
            // about the right height
            if(bounces>=numBounces && Abs(this->Y-48)<3)
                break;
            
            IflWaitframe(this, ghost);
        }
        
        // Change back
        SetAllDefenses(ghost, NPCDT_NONE);
        ghost->Defense[NPCD_BRANG]=NPCDT_BLOCK;
        ghost->Defense[NPCD_FIRE]=NPCDT_BLOCK;
        ghost->Defense[NPCD_HOOKSHOT]=NPCDT_BLOCK;
        ghost->Defense[NPCD_MAGIC]=NPCDT_BLOCK;
        ghost->Defense[NPCD_ARROW]=NPCDT_BLOCK;
        ghost->Damage-=2;
        
        Transform(this, ghost, IFL_WALK_COMBO, -1, 3, -1);
        hand1->Data=IFL_HAND1_COMBO;
        hand1->X=this->X-16;
        hand1->Y=this->Y+16;
        hand2->Data=IFL_HAND2_COMBO;
        hand2->X=this->X+48;
        hand2->Y=this->Y+16;
        
        this->Vx=0;
        this->Vy=0;
    }
    
    
    void IflWaitframe(ffc this, npc ghost)
    {
        ffc hand1=Screen->LoadFFC(IFL_HAND1_FFC);
        ffc hand2=Screen->LoadFFC(IFL_HAND2_FFC);
        
        GhostWaitframeF(this, ghost, false, false);
        
        // Check if dead - use 1000 HP as 0
        if(ghost->HP<=1000)
        {
            Die(this, ghost);
            
            Game->PlayMIDI(IFL_DUNGEON_MIDI);
            Quit();
        }
        // Make hands flash along with body
        else
        {
            hand1->CSet=this->CSet;
            hand2->CSet=this->CSet;
        }
    }
    
    
    // Show a death animation, then remove the enemy.
    void Die(ffc this, npc ghost)
    {
        lweapon explosion;
        ffc hand1=Screen->LoadFFC(IFL_HAND1_FFC);
        ffc hand2=Screen->LoadFFC(IFL_HAND2_FFC);
        
        // Stop moving, stop flashing, turn off collision detection
        ghost->CollDetection=false;
        RevertCSet(this, ghost);
        hand1->CSet=this->CSet;
        hand2->CSet=this->CSet;
        this->Vx=0;
        this->Vy=0;
        
        // Randomly placed harmless explosions - one every 1/4 second for 4 seconds
        for(int i=0; i<16; i++)
        {
            explosion=Screen->CreateLWeapon(LW_BOMBBLAST);
            explosion->X=Rand(this->X, this->X+16*this->TileWidth);
            explosion->Y=Rand(this->Y, this->Y+16*this->TileHeight);
            explosion->CollDetection=false;
            Waitframes(15);
        }
        
        // Kill NPC and clear combos
        ghost->HP=-1000;
        this->Data=0;
        hand1->Data=0;
        hand2->Data=0;
    }
}
